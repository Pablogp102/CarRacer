plugins {
    alias(libs.plugins.android.application)
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secrets = new Properties()
if (secretsPropertiesFile.exists()) {
    secrets.load(new FileInputStream(secretsPropertiesFile))
} else {
    println "secrets.properties file not found. Using default values or this will fail if secrets are required."
}



android {
    namespace 'com.carracer'
    compileSdk 35

    defaultConfig {
        applicationId "com.carracer"
        minSdk 34
        targetSdk 35
        versionCode 1
        versionName "1.0"
        def baseUrl = secrets['BASE_URL'] ?: "http://10.0.2.2:5000/"

        buildConfigField "String", "BASE_URL", "\"${baseUrl}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.appcompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    implementation libs.room.runtime
    implementation libs.room.common
    kapt libs.room.compiler
    implementation libs.play.services.location
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    implementation libs.logging.interceptor
    implementation libs.javax.inject
    implementation libs.hilt.android
    kapt libs.hilt.android.compiler
}